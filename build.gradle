plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id "pl.allegro.tech.build.axion-release" version "1.12.1"
    id "com.github.ben-manes.versions" version "0.36.0"
}

group = "com.kdabir.mksrc"

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()

    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5', {
        exclude group: 'org.codehaus.groovy'
    }

    testCompile "io.github.kdabir.directree:directree:0.3.2", {
        exclude group: 'org.codehaus.groovy'
    }
}

pluginBundle {
    website = 'https://github.com/kdabir/mksrc'
    vcsUrl = 'https://github.com/kdabir/mksrc'
    description = 'Generate source directories and settings.gradle file'
    tags = ['create', 'dirs', 'mkdir', 'sourcesets', 'settings.gradle']

    plugins {
        mksrc {
            id = 'com.kdabir.mksrc'
            displayName = 'Gradle plugin to create source directories'
        }
    }
}

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }

    localOnly = true

    hooks {
        post { context ->
            /// Pushing tags manually thru native git client because jgit requires authentication via a pop-up
            println "pushing tag: v${context.currentVersion}"

            exec {
                commandLine 'git', 'push', 'origin', "v${context.currentVersion}"
            }
        }
    }
}

project.version = scmVersion.version

