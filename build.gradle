plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id "pl.allegro.tech.build.axion-release" version "1.9.3"    
}

group = "com.kdabir.mksrc"

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude group: 'org.codehaus.groovy'
    }
}

pluginBundle {
    website = 'https://github.com/kdabir/mksrc'
    vcsUrl = 'https://github.com/kdabir/mksrc'
    description = 'Gradle plugin'
    tags = ['create', 'dirs', 'mkdir', 'sourcesets']

    plugins {
        mksrc {
            id = 'com.kdabir.mksrc'
            displayName = 'Gradle plugin to create source directories'
        }
    }
}

scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    
    localOnly = true

    hooks {
        post { context ->
            /// Pushing tags manually thru native git client because jgit requires authentication via a pop-up
            println "pushing tag: v${context.currentVersion}"

            exec {
                commandLine 'git', 'push', 'origin', "v${context.currentVersion}"
            }
        }
    }
}

tasks.publishPlugins.mustRunAfter('release')

task publish(dependsOn: ['release', 'publishPlugins'])